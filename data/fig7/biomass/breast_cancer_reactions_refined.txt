@rxn  RAS_inact  -->  RAS_act : p[V_Heregulin_HER3_HER2_p_RAS] * u[Heregulin_HER3_HER2_p] * u[RAS_inact] / ( p[K_Heregulin_HER3_HER2_p_RAS] + u[RAS_inact] ) || RAS_inact=1.00e02
@rxn  PI3K_inact  -->  PI3K_act : p[V_Heregulin_HER3_HER2_p_PI3K] * u[Heregulin_HER3_HER2_p] * u[PI3K_inact] / ( p[K_Heregulin_HER3_HER2_p_PI3K] + u[PI3K_inact] ) || PI3K_inact=1.00e02
@rxn  RAS_inact  -->  RAS_act : p[V_EGF_EGFR_p_RAS] * u[EGF_EGFR_p] * u[RAS_inact] / ( p[K_EGF_EGFR_p_RAS] + u[RAS_inact] )
@rxn  RAS_inact  -->  RAS_act : p[V_EGFR_EGFR_p_RAS] * u[EGFR_EGFR_p] * u[RAS_inact] / ( p[K_EGFR_EGFR_p_RAS] + u[RAS_inact] )
@rxn  PI3K_inact  -->  PI3K_act : p[V_EGFR_EGFR_p_PI3K] * u[EGFR_EGFR_p] * u[PI3K_inact] / ( p[K_EGFR_EGFR_p_PI3K] + u[PI3K_inact] )
@rxn  RAS_inact  -->  RAS_act : p[V_EGFR_HER2_p_RAS] * u[EGFR_HER2_p] * u[RAS_inact] / ( p[K_EGFR_HER2_p_RAS] + u[RAS_inact] )
@rxn  PI3K_inact  -->  PI3K_act : p[V_EGFR_HER2_p_PI3K] * u[EGFR_HER2_p] * u[PI3K_inact] / ( p[K_EGFR_HER2_p_PI3K] + u[PI3K_inact] )
@rxn  RAS_inact  -->  RAS_act : p[V_Shc_p_RAS] * u[Shc_p] * u[RAS_inact] / ( p[K_Shc_p_RAS] + u[RAS_inact] )
@rxn  RAF1_inact  -->  RAF1_act : p[V_RAS_RAF1] * u[RAS_act] * u[RAF1_inact] / ( p[K_RAS_RAF1] + u[RAF1_inact] ) || RAF1_inact=1.00e02
@rxn  AKT_inact  -->  AKT_p : p[V_PI3K_AKT] * u[PI3K_act] * u[AKT_inact] / ( p[K_PI3K_AKT] + u[AKT_inact] ) || AKT_inact=1.00e02
@rxn  AKT_inact  -->  AKT_p : p[V_PIP3_AKT] * u[PIP3] * u[AKT_inact] / ( p[K_PIP3_AKT] + u[AKT_inact] )
@rxn  RAF1_act  -->  RAF1_inact : p[V_SPRYiRAF1] * u[SPRY] * u[RAF1_act] / ( p[K_RAF1i] + u[RAF1_act] )
Heregulin binds HER3 <--> Heregulin_HER3 || HER3=1.00e02
Heregulin_HER3 binds HER2 <--> Heregulin_HER3_HER2 || HER2=1.00e02
Heregulin_HER3_HER2 is phosphorylated <--> Heregulin_HER3_HER2_p
EGF binds EGFR <--> EGF_EGFR || EGFR=1.00e02
EGF_EGFR dimerizes <--> EGF_EGFR_EGF_EGFR
EGF_EGFR_EGF_EGFR is phosphorylated <--> EGF_EGFR_p
EGFR dimerizes <--> EGFR_EGFR
EGFR_EGFR is phosphorylated <--> EGFR_EGFR_p
EGFR binds HER2 <--> EGFR_HER2
EGFR_HER2 is phosphorylated <--> EGFR_HER2_p
EGFR_EGFR_p phosphorylates Shc --> Shc_p || Shc=1.00e02
RAF1_act phosphorylates MEK --> MEK_p || MEK=1.00e02
MEK_p phosphorylates ERK --> ERK_p_cytoplasm || ERK=9.60e02
ERK_p_cytoplasm translocates <--> ERK_p_nucleus
ERK_p_cytoplasm phosphorylates RSK --> RSK_p || RSK=3.53e02
RSK_p phosphorylates cFOS --> cFOS_p || cFOS=1.00e02
cFOS_p is degraded | kf=1e-03
PI3K_act phosphorylates PIP2 --> PIP3 || PIP2=1.00e02
PTEN dephosphorylates PIP3 --> PIP2 || PTEN=1.00e02
PI3K_act phosphorylates AKT_inact --> AKT_p
AKT_p phosphorylates GSK3B --> GSK3B_p || GSK3B=1.00e02
AKT_p degrades CDKN1A
EGFR is degraded
ERK_p_nucleus transcribes DUSP
DUSP dephosphorylates ERK_p_cytoplasm --> ERK
ERK_p_nucleus transcribes SPRY
AKT_p transcribes CDKN1A
GSK3B degrades CDKN1A
Shc_p is degraded | kf=1e-03
MEK_p is degraded | kf=1e-03
RSK_p is degraded | kf=1e-03
AKT_p is degraded | kf=1e-03
GSK3B_p is degraded | kf=1e-03
DUSP is degraded | kf=1e-03
SPRY is degraded | kf=1e-03

# Added reactions
ERK_p_nucleus transcribes cFOS_mRNA
cFOS_mRNA is translated into cFOS
cFOS is degraded | kf=1e-02
cFOS_p is dephosphorylated --> cFOS | V=1e-03

@obs Phosphorylated_EGFR: (2 * u[EGF_EGFR_p] + 2 * u[EGFR_EGFR_p] + u[EGFR_HER2_p]) / (u[EGFR] + u[EGF_EGFR] + 2 * u[EGF_EGFR_EGF_EGFR] + 2 * u[EGF_EGFR_p] + 2 * u[EGFR_EGFR] +  2 * u[EGFR_EGFR_p] + u[EGFR_HER2] + u[EGFR_HER2_p])
@obs Phosphorylated_HER2: (u[Heregulin_HER3_HER2_p] + u[EGFR_HER2_p]) / (u[HER2] + u[Heregulin_HER3_HER2] + u[Heregulin_HER3_HER2_p] + u[EGFR_HER2] + u[EGFR_HER2_p])
@obs Phosphorylated_SHC: u[Shc_p] / (u[Shc] + u[Shc_p])
@obs Phosphorylated_MEK: u[MEK_p] / (u[MEK] + u[MEK_p])
@obs Phosphorylated_ERK: (u[ERK_p_cytoplasm] + u[ERK_p_nucleus]) / (u[ERK] + u[ERK_p_cytoplasm] + u[ERK_p_nucleus])

# Observables not used for parameter estimation
@obs Phosphorylated_HER3: u[Heregulin_HER3_HER2_p] / (u[HER3] + u[Heregulin_HER3] + u[Heregulin_HER3_HER2] + u[Heregulin_HER3_HER2_p])
@obs Phosphorylated_RSKw: u[RSK_p]
@obs Phosphorylated_cFos: u[cFOS_p]

@sim tspan: [0, 1800]
@sim condition EGF: init[EGF] = 1.0e01
@sim condition HRG: init[Heregulin] = 1.0e01
